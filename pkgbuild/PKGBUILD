# Maintainer: Kuan-Yen Chou <kuanyenchou@gmail.com>

pkgname=worldofwarcraft-git
pkgver=0
pkgrel=1
pkgdesc="World of Warcraft helper script"
arch=('any')
url="https://github.com/kyechou/worldofwarcraft"
license=('GPL3')
depends=('wine-staging' 'winetricks' 'bash' 'curl'
         'giflib' 'lib32-giflib' 'libpng' 'lib32-libpng' 'libldap' 'lib32-libldap'
         'gnutls' 'lib32-gnutls' 'mpg123' 'lib32-mpg123' 'openal' 'lib32-openal'
         'v4l-utils' 'lib32-v4l-utils' 'libpulse' 'lib32-libpulse'
         'libgpg-error' 'lib32-libgpg-error'
         'alsa-plugins' 'lib32-alsa-plugins' 'alsa-lib' 'lib32-alsa-lib'
         'libjpeg-turbo' 'lib32-libjpeg-turbo' 'sqlite' 'lib32-sqlite'
         'libxcomposite' 'lib32-libxcomposite' 'libxinerama' 'lib32-libxinerama'
         'libgcrypt' 'lib32-libgcrypt' 'ncurses' 'lib32-ncurses'
         'opencl-icd-loader' 'lib32-opencl-icd-loader' 'libxslt' 'lib32-libxslt'
         'libva' 'lib32-libva' 'gtk3' 'lib32-gtk3'
         'gst-plugins-base-libs' 'lib32-gst-plugins-base-libs'
         'vulkan-icd-loader' 'lib32-vulkan-icd-loader' 'vulkan-driver'
         'lib32-vulkan-driver')
makedepends=()
optdepends=("lib32-amdvlk: AMD Vulkan driver"
            "lib32-nvidia-utils: NVIDIA Vulkan driver"
            "lib32-vulkan-intel: Intel's Vulkan mesa driver"
            "lib32-vulkan-radeon: Radeon's Vulkan mesa driver")
provides=('worldofwarcraft')
conflicts=('worldofwarcraft')
source=("$pkgname"::'git+https://github.com/kyechou/worldofwarcraft')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    if git describe --long --tags >/dev/null 2>&1; then
        git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
    else
        printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
    fi
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install
}
